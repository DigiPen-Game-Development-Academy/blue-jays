//Author: Eli Miller
//Purpose: Controls what the buttons do.

enum Menu {MAIN, CREDITS, LEVEL, ACHIEVEMENTS}

class ParticleToggleEvent : ZilchEvent
{
    var Name : String = "ParticleToggle";
}

class MenuController : ZilchComponent
{
    sends ParticleToggleEvent : ParticleToggleEvent;
    [Property] var Menu : Menu;
    function Initialize(init : CogInitializer)
    {
        if(this.Menu == Menu.MAIN)
        {
            this.MainMenu();
        }
        else if(this.Menu == Menu.CREDITS)
        {
            this.Credits();
        }
        else if(this.Menu == Menu.LEVEL)
        {
            this.Level();
        }
        else if(this.Menu == Menu.ACHIEVEMENTS)
        {
            this.Achievements();
        }
    }
    
    function SetTC(name : String, del : delegate())
    {
        this.Space.FindObjectByName(name).Button.ToCall = del;
    }
    
    function MainMenu()
    {
        this.SetTC("StartGame", this.LoadHub);
        this.SetTC("Achievements", this.LoadAchievements);
        this.SetTC("Credits", this.LoadCredits);
        this.SetTC("Quit", this.GameSession.Quit);
    }
    
    function Credits()
    {
        this.SetTC("Menu", this.LoadMenu);
    }
    
    function Level()
    {
        this.SetTC("Menu", this.LoadMenu);
        this.SetTC("Resume", this.Resume);
        this.SetTC("Options", this.ToggleOptions);
        this.SetTC("Back", this.ToggleOptions);
        this.SetTC("DisableParticles", this.ToggleParticles);
    }
    
    function Achievements()
    {
        this.SetTC("Menu", this.LoadMenu);
        this.SetTC("Reset", this.ResetAchievements);
    }
    
    function ResetAchievements()
    {
        Zero.ObjectStore.ClearStore();
        this.Space.LoadLevel(Level.Achievements);
    }
    
    function Resume()
    {
        this.Space.FindObjectByName("PauseOverlay").OverlayToggle.Toggle();
        this.Space.OtherSpace.Ref.TimeSpace.TimeScale = 1;
        this.Space.SoundSpace.Volume = this.Space.FindObjectByName("Circle").Slider.GetPercentage();
        Console.WriteLine(this.Space.FindObjectByName("Circle").Slider.GetPercentage());
    }
    
    function ToggleOptions()
    {
        this.Space.FindObjectByName("PauseOverlay").OverlayToggle.Toggle();
        this.Space.FindObjectByName("OptionsOverlay").OverlayToggle.Toggle();
    }
    
    function ToggleParticles()
    {
        var e = new ParticleToggleEvent();
        this.Space.OtherSpace.Ref.DispatchEvent(Events.ParticleToggleEvent, e);
        this.Space.OtherSpace.Ref.Globals.Particles = !this.Space.OtherSpace.Ref.Globals.Particles;
    }
    
    function LoadCredits()
    {
        this.Space.LoadLevel(Level.Find("Credits"));
    }
    
    function LoadMenu()
    {
        this.Space.LoadLevel(Level.Find("MainMenu"));
        this.Space.TimeSpace.TimeScale = 1;
    }
    
    function LoadHub()
    {
        this.Space.OtherSpace.Ref.LoadLevel(Level.Find("Hub"));
        this.Space.LoadLevel(Level.Find("HUDLevel"));
    }
    
    function LoadAchievements()
    {
        this.Space.LoadLevel(Level.Find("Achievements"));
    }
}
