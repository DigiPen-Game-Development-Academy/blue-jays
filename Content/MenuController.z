//Author: Eli Miller
//Purpose: Controls what the buttons do.

enum Menu {MAIN, CREDITS, LEVEL, ACHIEVEMENTS}

class ParticleToggleEvent : ZilchEvent
{
    var Name : String = "ParticleToggle";
}

class MenuController : ZilchComponent
{
    sends ParticleToggleEvent : ParticleToggleEvent;
    [Property] var Menu : Menu;
    function Initialize(init : CogInitializer)
    {
        if(this.Menu == Menu.MAIN)
        {
            this.MainMenu();
        }
        else if(this.Menu == Menu.CREDITS)
        {
            this.Credits();
        }
        else if(this.Menu == Menu.LEVEL)
        {
            this.Level();
        }
        else if(this.Menu == Menu.ACHIEVEMENTS)
        {
            this.Achievements();
        }
    }
    
    function MainMenu()
    {
        this.Space.FindObjectByName("StartGame").Button.ToCall = this.LoadHub;
        this.Space.FindObjectByName("Achievements").Button.ToCall = this.LoadAchievements;
        this.Space.FindObjectByName("Credits").Button.ToCall = this.LoadCredits;
        this.Space.FindObjectByName("Quit").Button.ToCall = this.GameSession.Quit;
    }
    
    function Credits()
    {
        this.Space.FindObjectByName("Menu").Button.ToCall = this.LoadMenu;
    }
    
    function Level()
    {
        this.Space.FindObjectByName("Menu").Button.ToCall = this.LoadMenu;
        this.Space.FindObjectByName("Resume").Button.ToCall = this.Resume;
        this.Space.FindObjectByName("Options").Button.ToCall = this.ToggleOptions;
        this.Space.FindObjectByName("Back").Button.ToCall = this.ToggleOptions;
        this.Space.FindObjectByName("DisableParticles").Button.ToCall = this.ToggleParticles;
    }
    
    function Achievements()
    {
        this.Space.FindObjectByName("Menu").Button.ToCall = this.LoadMenu;
        this.Space.FindObjectByName("Reset").Button.ToCall = this.ResetAchievements;
    }
    
    function ResetAchievements()
    {
        Zero.ObjectStore.ClearStore();
        this.Space.LoadLevel(Level.Achievements);
    }
    
    function Resume()
    {
        this.Space.FindObjectByName("PauseOverlay").OverlayToggle.Toggle();
        this.Space.OtherSpace.Ref.TimeSpace.TimeScale = 1;
        this.Space.SoundSpace.Volume = this.Space.FindObjectByName("Circle").Slider.GetPercentage();
        Console.WriteLine(this.Space.FindObjectByName("Circle").Slider.GetPercentage());
    }
    
    function ToggleOptions()
    {
        this.Space.FindObjectByName("PauseOverlay").OverlayToggle.Toggle();
        this.Space.FindObjectByName("OptionsOverlay").OverlayToggle.Toggle();
    }
    
    function ToggleParticles()
    {
        var e = new ParticleToggleEvent();
        this.Space.OtherSpace.Ref.DispatchEvent(Events.ParticleToggleEvent, e);
        this.Space.OtherSpace.Ref.Globals.Particles = !this.Space.OtherSpace.Ref.Globals.Particles;
    }
    
    function LoadCharSelect()
    {
        this.Space.LoadLevel(Level.Find("CharacterSelect"));
    }
    
    function LoadCredits()
    {
        this.Space.LoadLevel(Level.Find("Credits"));
    }
    
    function LoadMenu()
    {
        this.Space.LoadLevel(Level.Find("MainMenu"));
        this.Space.TimeSpace.TimeScale = 1;
    }
    
    function LoadFool()
    {
        this.Space.LoadLevel(Level.Find("Rafters"));
    }
    
    function LoadHub()
    {
        this.Space.LoadLevel(Level.Find("Hub"));
    }
    
    function LoadAchievements()
    {
        this.Space.LoadLevel(Level.Find("Achievements"));
    }
}
