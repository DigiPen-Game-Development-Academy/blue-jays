//Author: Eli Miller
//Purpose: Tracks, controls, and unlocks achievements.

//The event on which an achievement is unocked.
enum AchEvent {DEATH, BEAT_FOOL, BEAT_LOVERS, WIN, CHECK}

//The requirement needed for the achievement.
enum AchRequire {DEATHS, HEALTH, KNIGHT_SPAWNS, STAT_SPAWNS, UNIQUE_SPAWNS, SPAWNS,
    TIME, LEVEL_TIME, TOTAL_RUNS, FOOL_BEATEN, LOVERS_BEATEN, WINS}

//The class for each achievement.
class Achievement
{
    var Name : String = "Unnamed";
    var Objective : String = "No Objective";
    var Flavor : String = "Mysterious...";
    var Unlocked : Boolean = false;
    var Sprite : SpriteSource;
    var Type : AchRequire = AchRequire.TIME;
    var Min : Real = 0;
    var Max : Real = 0;
    var AchEvent : AchEvent;
    constructor(name : String, objective : String, flavor : String, req : AchRequire,
        min : Real, max : Real, e : AchEvent, s : SpriteSource)
    {
        this.Name = name;
        this.Objective = objective;
        this.Flavor = flavor;
        this.Sprite = s;
        this.Type = req;
        this.Min = min;
        this.Max = max;
        this.AchEvent = e;
    }
}

class AchievementTracker : ZilchComponent
{
    //We need these arrays for easier access to the achievements that need to be checked for each event.
    var DeathAchievements : Array[Achievement] = Array[Achievement]();
    var FoolAchievements : Array[Achievement] = Array[Achievement]();
    var LoversAchievements : Array[Achievement] = Array[Achievement]();
    var WinAchievements : Array[Achievement] = Array[Achievement]();
    var CheckAchievements : Array[Achievement] = Array[Achievement]();
    var AllAchievements : Array[Achievement] = Array[Achievement]();
    
    //These have requirements that aren't fufilled with an AchRequire.
    var CreditsAchievement : Achievement;
    var CheatAchievement : Achievement;
    
    function Initialize(init : CogInitializer)
    {
        this.AddAchievements();
        //Loads the unlocked achievements from the SavedStats.
        for(var i = 0; i < this.Space.Globals.SavedStats.SavedStats.AchUnlocks.Count; i += 1)
        {
            var b = this.Space.Globals.SavedStats.SavedStats.AchUnlocks[i];
            this.AllAchievements[i].Unlocked = b == "y"[0];
        }
        Zero.Connect(this.Space, Events.LevelStarted, this.OnLevelStart);
    }
    
    //Construct the text shown in the progress portion of the text box.
    function GetProgressText(ach : Achievement) : String
    {
        if(ach.Unlocked)
        {
            return "Complete";
        }
        var str = "";
        str = "`this.GetAchRequire(ach.Type)`/`ach.Min`";
        var ar = ach.Type;
        if(ar == AchRequire.DEATHS)
        {
            str = "`str` Deaths";
        }
        else if(ar == AchRequire.WINS)
        {
            str = "`str` Wins";
        }
        else if(ar == AchRequire.HEALTH)
        {
            str = "Incomplete";
        }
        else if(ar == AchRequire.SPAWNS)
        {
            str = "`str` Turrets Spawned";
        }
        else if(ar == AchRequire.KNIGHT_SPAWNS)
        {
            str = "`str` Knights Spawned";
        }
        else if(ar == AchRequire.STAT_SPAWNS)
        {
            str = "`str` Towers Spawned";
        }
        else if(ar == AchRequire.UNIQUE_SPAWNS)
        {
            str = "`str` Chariots Spawned";
        }
        else if(ar == AchRequire.TOTAL_RUNS)
        {
            str = "`str` Runs";
        }
        else if(ar == AchRequire.FOOL_BEATEN)
        {
            str = "`str` Fools Beaten";
        }
        else if(ar == AchRequire.LOVERS_BEATEN)
        {
            str = "`str` Lovers Beaten";
        }
        else if(ar == AchRequire.TIME)
        {
            str = "Incomplete";
        }
        else
        {
            str = "Incomplete";
        }
        return str;
    }
    
    function AddAchievement(name : String, objective : String, flavor : String, req : AchRequire,
        min : Real, max : Real, e : AchEvent, s : SpriteSource)
    {
        var ach = Achievement(name, objective, flavor, req,
        min, max, e, s);
        
        //Add this achievement to the respective array.
        if(e==AchEvent.DEATH)
        {
            this.DeathAchievements.Add(ach);
        }
        else if(e==AchEvent.BEAT_FOOL)
        {
            this.FoolAchievements.Add(ach);
        }
        else if(e==AchEvent.BEAT_LOVERS)
        {
            this.LoversAchievements.Add(ach);
        }
        else if(e==AchEvent.WIN)
        {
            this.WinAchievements.Add(ach);
        }
        else
        {
            this.CheckAchievements.Add(ach);
        }
        this.AllAchievements.Add(ach);
    }
    
    //Checks to see if the achievement is complete, and updates it accordingly.
    function Check(ach : Achievement)
    {
        if (ach.Unlocked)
        {
            return;
        }
        var num = this.GetAchRequire(ach.Type);
        ach.Unlocked = num <= ach.Max && num >= ach.Min;
    }
    
    //Gets the number corresponding to an AchRequire.
    function GetAchRequire(ar : AchRequire) : Real
    {
        var stats = this.Space.Globals.SavedStats.SavedStats;
        if(ar == AchRequire.DEATHS)
        {
            return stats.Deaths;
        }
        else if(ar == AchRequire.WINS)
        {
            return stats.Wins;
        }
        else if(ar == AchRequire.HEALTH)
        {
            return stats.PrevHealth;
        }
        else if(ar == AchRequire.SPAWNS)
        {
            return stats.Spawns;
        }
        else if(ar == AchRequire.KNIGHT_SPAWNS)
        {
            return stats.KnightSpawns;
        }
        else if(ar == AchRequire.STAT_SPAWNS)
        {
            return stats.StationarySpawns;
        }
        else if(ar == AchRequire.UNIQUE_SPAWNS)
        {
            return stats.UniqueSpawns;
        }
        else if(ar == AchRequire.TOTAL_RUNS)
        {
            return stats.TotalRuns;
        }
        else if(ar == AchRequire.FOOL_BEATEN)
        {
            return stats.FoolsBeaten;
        }
        else if(ar == AchRequire.LOVERS_BEATEN)
        {
            return stats.LoversBeaten;
        }
        else if(ar == AchRequire.TIME)
        {
            return this.Space.Timer.TimeSinceFoolLoad;
        }
        return 0;
    }
    
    //Used to check achievements when necessary.
    function OnLevelStart(event : GameEvent)
    {
        var achievements : Array[Achievement] = null;
        if(event.LevelName == "Win")
        {
            achievements = this.WinAchievements;
        }
        else if(event.LevelName == "Lose")
        {
            achievements = this.DeathAchievements;
        }
        else if(event.LevelName == "Lovers")
        {
            achievements = this.FoolAchievements;
        }
        else if(event.LevelName == "Death")
        {
            achievements = this.LoversAchievements;
        }
        else if(event.LevelName == "Achievements")
        {
            achievements = this.CheckAchievements;
        }
        else if(event.LevelName == "Credits")
        {
            this.CreditsAchievement.Unlocked = true;
            this.Space.Globals.StoreStats();
        }
        if(achievements != null)
        {
            foreach(var achievement in achievements)
            {
                this.Check(achievement);
            }
            this.Space.Globals.StoreStats();
        }
        if(event.LevelName == "Achievements")
        {
            this.LevelSettings.DrawAchievements.Draw(this.AllAchievements);
        }
    }
    
    //Creates all achievements.
    function AddAchievements()
    {
        this.AddAchievement("First Death", "Die 1 time.", "When's the funeral?",
            AchRequire.DEATHS, 1, 100000, AchEvent.CHECK, SpriteSource.Tombstone);
        this.AddAchievement("RIP", "Die 5 times.", "Do your worst.",
            AchRequire.DEATHS, 5, 100000, AchEvent.CHECK, SpriteSource.Tombstone);
        this.AddAchievement("Graveyard", "Die 20 times.", "Having some trouble?",
            AchRequire.DEATHS, 20, 100000, AchEvent.CHECK, SpriteSource.Tombstone);
        this.AddAchievement("Determined", "Die 50 times.", "LET THE BODIES HIT THE FLOOR",
            AchRequire.DEATHS, 50, 100000, AchEvent.CHECK, SpriteSource.Tombstone);
        this.AddAchievement("Fool Beaten", "Beat the Fool.", "Hope you enjoyed the show!",
            AchRequire.FOOL_BEATEN, 1, 100000, AchEvent.CHECK, SpriteSource.BronzeFool);
        this.AddAchievement("Encore!", "Beat the Fool 5 times.", "I'll take that as a yes.",
            AchRequire.FOOL_BEATEN, 5, 100000, AchEvent.CHECK, SpriteSource.SilverFool);
        this.AddAchievement("Perfect Fool Run", "Beat the Fool with full health.", "What a fool!",
            AchRequire.HEALTH, 100, 100, AchEvent.BEAT_FOOL, SpriteSource.BeatTheFool);
        this.AddAchievement("Quick Show", "Beat the Fool in a minute.", "Hope you enjoyed the sh-",
            AchRequire.TIME, 0, 60, AchEvent.BEAT_FOOL, SpriteSource.Clock);
        this.AddAchievement("Not Fooling Around", "Beat the Fool in 30 seconds.", "Hope y-",
            AchRequire.TIME, 0, 30, AchEvent.BEAT_FOOL, SpriteSource.Clock);
        this.AddAchievement("Stationary Spawner", "Spawn 5 towers.", "Someday you might land a hit...",
            AchRequire.STAT_SPAWNS, 5, 100000, AchEvent.CHECK, SpriteSource.Towers);
        this.AddAchievement("Stationary Master", "Spawn 25 towers.", "Did that 500 fate pay off?",
            AchRequire.STAT_SPAWNS, 25, 100000, AchEvent.CHECK, SpriteSource.Towers);
        this.AddAchievement("Unique Spawner", "Spawn 5 chariots.", "Look at it go!",
            AchRequire.UNIQUE_SPAWNS, 5, 100000, AchEvent.CHECK, SpriteSource.Chariot);
        this.AddAchievement("Unique Master", "Spawn 25 chariots.", "Never stops being satisfying.",
            AchRequire.UNIQUE_SPAWNS, 25, 100000, AchEvent.CHECK, SpriteSource.Chariot);
        this.AddAchievement("Lovers Beaten", "Beat the Lovers.", "Don't you feel a bit guilty?",
            AchRequire.LOVERS_BEATEN, 1, 100000, AchEvent.CHECK, SpriteSource.MaleLover);
        this.AddAchievement("All You Need Is Love", "Beat the Lovers 5 times.", "Ruthless.",
            AchRequire.LOVERS_BEATEN, 5, 100000, AchEvent.CHECK, SpriteSource.FemaleLover);
        this.AddAchievement("Perfect Lovers Run", "Beat the Lovers with full health.", "Love is blind...",
            AchRequire.HEALTH, 100, 100, AchEvent.BEAT_LOVERS, SpriteSource.LoverBG);
        this.AddAchievement("One Minute In Heaven", "Beat the Lovers in a minute.", "It wasn't meant to last.",
            AchRequire.TIME, 0, 60, AchEvent.BEAT_LOVERS, SpriteSource.Clock);
        this.AddAchievement("I Don't Wanna See That", "Beat the Lovers in 30 seconds.", "Eww, kissing!",
            AchRequire.TIME, 0, 30, AchEvent.BEAT_LOVERS, SpriteSource.Clock);
        this.AddAchievement("Knight Spawner", "Spawn 10 knights.", "Not quite an army...",
            AchRequire.KNIGHT_SPAWNS, 10, 100000, AchEvent.CHECK, SpriteSource.Knight_swords);
        this.AddAchievement("Knight Master", "Spawn 50 knights.", "Good enough.",
            AchRequire.KNIGHT_SPAWNS, 50, 100000, AchEvent.CHECK, SpriteSource.Knight_swords);
        this.AddAchievement("Novice Spawner", "Spawn 15 turrets.", "Congrats! You clicked!",
            AchRequire.SPAWNS, 15, 100000, AchEvent.CHECK, SpriteSource.Bomb);
        this.AddAchievement("Master Spawner", "Spawn 75 turrets.", "Carpal tunnel awaits.",
            AchRequire.SPAWNS, 75, 100000, AchEvent.CHECK, SpriteSource.Bomb);
        this.AddAchievement("Winner, Winner", "Beat Death.", "They said it was impossible.",
            AchRequire.WINS, 1, 100000, AchEvent.CHECK, SpriteSource.Death_crow);
        this.AddAchievement("Winner, Winner, Winner, Winner...", "Beat Death 5 times.", "They said it was possible.",
            AchRequire.WINS, 5, 100000, AchEvent.CHECK, SpriteSource.Death_crow);
        this.AddAchievement("Perfect Death Run", "Beat Death without taking damage.", "Definitely not dying now.",
            AchRequire.HEALTH, 100, 100, AchEvent.WIN, SpriteSource.Death_crow);
        this.AddAchievement("A Quick Death", "Beat Death in a minute.", "Death: wow ur a dum hacker",
            AchRequire.TIME, 0, 60, AchEvent.WIN, SpriteSource.Death_crow);
        this.AddAchievement("Anticlimactic", "Beat Death in 30 seconds.", "Death: stupid spawnkiller",
            AchRequire.TIME, 0, 30, AchEvent.WIN, SpriteSource.Death_crow);
        this.AddAchievement("First Run", "Play the game 1 time.", "That was dumb.",
            AchRequire.TOTAL_RUNS, 1, 100000, AchEvent.CHECK, SpriteSource.One);
        this.AddAchievement("Novice Player", "Play the game 5 times.", "This is boring.",
            AchRequire.TOTAL_RUNS, 5, 100000, AchEvent.CHECK, SpriteSource.Five);
        this.AddAchievement("Experienced Player", "Play the game 20 times.", "This game sucks.",
            AchRequire.TOTAL_RUNS, 20, 100000, AchEvent.CHECK, SpriteSource.Twenty);
        this.AddAchievement("Expert Player", "Play the game 50 times.", "I hate this game.",
            AchRequire.TOTAL_RUNS, 50, 100000, AchEvent.CHECK, SpriteSource.Fiftey);
        this.AddAchievement("Speedrun", "Beat the game in 2 minutes.", "Let's just get it over with.",
            AchRequire.TIME, 0, 120, AchEvent.WIN, SpriteSource.YouWin);
        this.AddAchievement("Minute To Win It", "Beat the game in a minute.", "That's fast.",
            AchRequire.TIME, 0, 60, AchEvent.WIN, SpriteSource.YouWin1);
        this.CreditsAchievement = Achievement("Thanks!", "Look at the credits.", "Eli is the only important one.\\s",
            AchRequire.TIME, -1, -1, AchEvent.CHECK, SpriteSource.Hacker);
        this.AllAchievements.Add(this.CreditsAchievement);
        this.CheatAchievement = Achievement("Cheater", "Use a cheat.", "Now beat it the hard way.",
            AchRequire.TIME, -1, -1, AchEvent.CHECK, SpriteSource.Hacker);
        this.AllAchievements.Add(this.CheatAchievement);
        this.AddAchievement("Hacker", "Hack the game.", "That was mean.",
            AchRequire.TIME, -1, -1, AchEvent.CHECK, SpriteSource.Hacker);
    }
}