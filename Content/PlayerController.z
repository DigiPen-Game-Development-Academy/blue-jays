// Author: Eli Miller
// Purpose: Takes keyboard input to control the player's movement and turret placement.

class PlayerController : ZilchComponent
{
    [Dependency] var RigidBody : RigidBody;
    
    [Property] var Speed : Real = 10;
    [Property] var Acceleration : Real = 5;
    [Property] var Deceleration : Real = 10;
    
    var Reversed : Boolean;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    }
    
    function OnLogicUpdate(event : UpdateEvent)
    {
        var vel = Real2();
        if(Zero.Keyboard.KeyIsDown(Keys.D)) vel.X += 1;
        if(Zero.Keyboard.KeyIsDown(Keys.A)) vel.X -= 1;
        if(Zero.Keyboard.KeyIsDown(Keys.W)) vel.Y += 1;
        if(Zero.Keyboard.KeyIsDown(Keys.S)) vel.Y -= 1;
        
        var chariotExists = this.GameSession.Globals.SingletonChariot != null;
        
        if (chariotExists || vel == Real2.Zero)
        {
            if (this.Owner.Sprite.SpriteSource != SpriteSource.Swords)
            {
                this.Owner.Sprite.SpriteSource = SpriteSource.Swords;
            }
        }
        
        if (!chariotExists && vel != Real2.Zero && Math.LengthSq(this.RigidBody.Velocity) > 0.1)
        {
            if (this.Owner.Sprite.SpriteSource != SpriteSource.KingOS_Run)
            {
                this.Owner.Sprite.SpriteSource = SpriteSource.KingOS_Run;
            }
        }
        
        if (vel == Real2.Zero)
        {
            this.RigidBody.Velocity = Math.Lerp(this.RigidBody.Velocity, Real3.Zero, this.Deceleration * event.Dt);
        }
        else
        {
            if (this.Reversed) vel = -vel;
            this.RigidBody.Velocity = Math.Lerp(this.RigidBody.Velocity, Real3(vel * this.Speed, 0), this.Acceleration * event.Dt);
        }
    }
}