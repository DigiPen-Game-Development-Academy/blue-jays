//Author: Eli Miller
//Purpose: Gives an object health.

class HP : ZilchComponent
{
    [Property] var MaxHealth : Real = 100;
    
    var Health : Real;
    var FlashTimer : Real = 0;
    var Flashed : Boolean = false;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        this.Health = this.MaxHealth;
    }
    
    function OnLogicUpdate(event : UpdateEvent)
    {
        if(this.Flashed)
        {
            this.FlashTimer -= event.Dt;
            if(this.FlashTimer <= 0)
            {
                this.Flashed = false;
                this.Owner.Sprite.Color = Real4(1,1,1,1);
            }
        }
    }
    
    // Damages the entity
    function Damage(damage : Real)
    {
        this.Owner.Sprite.Color = Real4(1,0,0,1);
        this.Flashed = true;
        this.FlashTimer = 0.25;
        this.Space.SoundSpace.PlayCue(SoundCue.Damage);
        this.Health -= damage;
        if(this.Health <= 0)
        {
            if(this.Owner.Name == "Player")
            {
                this.Space.Globals.SavedStats.SavedStats.Deaths += 1;
                this.Space.Globals.SavedStats.SavedStats.TotalRuns += 1;
                this.Space.Globals.SavedStats.SavedStats.PrevHealth = this.Health;
                this.Space.LoadLevel(Level.Lose);
            }
            else if(this.Owner.ArchetypeName == "Fool")
            {
                this.Space.Globals.SavedStats.SavedStats.FoolsBeaten += 1;
                this.Space.Globals.SavedStats.SavedStats.Wins += 1;
                this.Space.Globals.SavedStats.SavedStats.TotalRuns += 1;
                this.Space.LoadLevel(Level.Win);
            }
            else if(this.Owner.ArchetypeName == "Pigeon")
            {
                this.Owner.Destroy();
            }
            this.Space.Globals.StoreStats();
        }
    }
}